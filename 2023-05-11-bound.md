------------------------------------------------------
title: Bound: Split into Something like a Free Monad
summary: Explaining a take on the bound library that uses something like a free monad
tags: haskell
draft: false
------------------------------------------------------

Recently my friend [Jonas Carpay](https://jonascarpay.com/) has been teaching
me about the Haskell package [`bound`](https://hackage.haskell.org/package/bound),
and the fundamental problem it is trying to solve.  `bound` gives a good solution
for defining a lambda calculus in Haskell, specifically with how to differentiate
between bound and free variables.

While playing around with the ideas from `bound`, I found a neat way of defining
a datatype for a lambda calculus using something like a free monad.

This post explains a little background for `bound`, and then shows my own
definition using the free-monad-like structure.

## Lambda Calculus

Most Haskellers will be familiar with lambda calculus.  Even if you've never
studied it, you will recognize it just from being familiar with Haskell.

An example lambda calculus expression might look like the following:

```
\x -> \y -> a (x y)
```

Lambda calculus has variables, function application, and lambda functions.  All
things that you'll be familiar with from Haskell.

If you wanted to write a data type in Haskell to represent a lambda calculus
expression, what might it look like?  You might start with something like this:

```haskell
data ExpTooSimp
  = ExpTooSimpVar String
    -- ^ This refers to a variable, like @x@.
  | ExpTooSimpApp ExpTooSimp ExpTooSimp
    -- ^ This refers to a function application, like @f x@.
  | ExpTooSimpLam String ExpTooSimp
    -- ^ This refers to a lambda function, like @\x -> _@.
```

So our above example lambda calculus expression might look like the following
in Haskell:

```haskell
ExpTooSimpLam "x"
  ( ExpTooSimpLam "y"
      ( ExpTooSimpApp
          (ExpTooSimpVar "a")
          (ExpTooSimpApp (ExpTooSimpVar "x") (ExpTooSimpVar "y"))
      )
  )
```

One generalization we can make is turning the `String` into a parameter:

```haskell
data ExpSimp a
  = ExpSimpVar a
    -- ^ This refers to a variable, like @x@.
  | ExpSimpApp (ExpSimp a) (ExpSimp a)
    -- ^ This refers to a function application, like @f x@.
  | ExpSimpLam a (ExpSimp a)
    -- ^ This refers to a lambda function, like @\x -> _@.
```

Then, instead of having `ExpTooSimp`, we have `ExpSimp String`.

This datatype is certainly easy to understand, but it leaves a little to be
desired.

## Bound vs Free Variables

One thing that the above Haskell datatype doesn't address is our desire to
differentiate between bound and free variables.

For instance, in the expression:

```
\x -> \y -> a (x y)
```

the `x` and `y` are bound variables, while the `a` is a free variable.

How would we change our datatype to take this bound-vs-free information into
account?

The article [Bound](https://www.schoolofhaskell.com/user/edwardk/bound) by
Edward Kmett explains a bunch of different ways to express this in Haskell.
The article works towards explaining a specific technique, and why it is
better than most other techniques.  This article refers to the general
technique as *Bird and Paterson*, which is introduced in the first half of
[De Bruijn notation as a nested datatype](http://www.cs.uwyo.edu/~jlc/courses/5000_fall_08/debruijn_as_nested_datatype.pdf).
The [`bound`](https://hackage.haskell.org/package/bound) Haskell package then
improves on this technique in a few ways, and wraps it up in a library.

The general idea from the *Bird and Paterson* technique is to take our simple
expression datatype:

```haskell
data ExpSimp a
  = ExpSimpVar a
    -- ^ This refers to a variable, like @x@.
  | ExpSimpApp (ExpSimp a) (ExpSimp a)
    -- ^ This refers to a function application, like @f x@.
  | ExpSimpLam a (ExpSimp a)
    -- ^ This refers to a lambda function, like @\x -> _@.
```

and rewrite it like this:

```haskell
data Exp a
  = ExpVar a
    -- ^ This refers to a variable, like @x@.
  | ExpApp (Exp a) (Exp a)
    -- ^ This refers to a function application, like @f x@.
  | ExpLam (Exp (Maybe a))
    -- ^ This refers to a lambda function, like @\x -> _@.
```

The `ExpVar` and `ExpApp` cases haven't changed, but the `ExpLam` case needs
some explaining.

First off, in the new `ExpLam`, where has the variable name gone?  Why isn't this
something like `ExpLam a (Exp a)`?

The insight is that in `ExpLam (Exp (Maybe a))`, we're recursing on `Exp`, but
on every recursion, we add a new layer of `Maybe` to `a`.  It will help to look
at a few examples.

Using `Exp String`, here's a top-level free variable without any lambdas involved.

|   | lambda calculus | haskell |
|-----|-----|-----|
| top-level free variable | `a` | `ExpVar "a"` |

This should be straight-forward.

How about a simple lambda expression, with a free variable, and with a bound
variable:

|   | lambda calculus | haskell |
|-----|-----|-----|
| bound variable | `\x -> x` | `ExpLam (ExpVar Nothing)` |
| free variable | `\x -> a` | `ExpLam (ExpVar (Just "a"))` |

You can see that within one layer of `ExpLam`, `ExpVar` now takes a type
of `Maybe String`.  `Just` refers to a free variable, while `Nothing` refers to
the closest bound variable.

This is pretty neat.  We now have a way to differentiate free and bound
variables.  Bound variables don't even need to carry around a variable name
(once a variable is bound, it doesn't really matter what its name is)!

What does this look like with more layers of lambdas?  Here are a few more examples:

|   | lambda calculus | haskell |
|-----|-----|-----|
| var bound to inner lambda | `\x -> \y -> y` | `ExpLam (ExpLam (ExpVar Nothing))` |
| var bound to outer lambda | `\x -> \y -> x` | `ExpLam (ExpLam (ExpVar (Just Nothing)))` |
| free var | `\x -> \y -> a` | `ExpLam (ExpLam (ExpVar (Just (Just "a"))))` |

You can see here that within two layers of lambdas, `ExpVar` now takes a type
of `Maybe (Maybe String)`.  So when adding each layer of lambda, we have to add
a new layer of `Maybe`.

With two layers of lambda (and two corresponding layers of `Maybe`), you can
see that `Nothing` refers to the closest binding lambda, `Just Nothing` refers
to the next closest binding lambda, and `Just (Just v)` refers to a free
variable.

While this is certainly clever, why do we prefer `Exp` over `ExpSimp`?

## Monad and Foldable for Exp

`Exp` is nice because it has straight-forward a `Monad`, `Foldable`, and
`Traversable` instance that allows us to manipulate free (non-bound)
variables.

Here's what these instances look like:

```haskell
data Exp a
  = ExpVar a
  | ExpApp (Exp a) (Exp a)
  | ExpLam (Exp (Maybe a))
  deriving stock (Functor, Foldable, Traversable)

-- A simple Applicative instance
instance Applicative Exp where pure = ExpVar ; (<*>) = ap

instance Monad Exp where
  (>>=) :: Exp a -> (a -> Exp b) -> Exp b
  e >>= k =
    case e of
      ExpVar a -> k a
      ExpApp f a -> ExpApp (f >>= k) (a >>= k)
      ExpLam l -> ExpLam (l >>= traverse k)
```

Let's look at how we can use each of these instances in turn.
First, here's an example expression we can play around with:

```haskell
exampleExp1 :: Exp String
exampleExp1 =
  ExpLam $
    ExpApp
      (ExpVar (Just "a"))
      (ExpLam $
        ExpApp
          (ExpVar (Just Nothing))
          (ExpVar (Just (Just "b"))
      )
```

This corresponds to the Lambda calculus expression:

```
\x -> a (\y -> x b)
```

First, let's look at `Functor`.  The `Functor` instance gives us the ability to
modify free variables:

```haskell
> fmap (ord . head :: String -> Int) exampleExp1 :: Exp Int
ExpLam $
  ExpApp
    (ExpVar (Just 97))
    (ExpLam $
      ExpApp
        (ExpVar (Just Nothing))
        (ExpVar (Just (Just 98))
    )
```

This result corresponds to a lambda expression that would look something like
the following[^2]:

```
\x -> 97 (\y -> x 98)
```

Note that the bound `x` variable is not affected in any way, only the free `a`
and `b` variables.

The `Monad` instance gives us a way to substitute free variables.  For instance,
in our running example, let's say we want to substitute in the identity function
in place of the free variable `a`.

In our example `\x -> a (\y -> x b)`, we want to substitute in `\z -> z`.  The
result should look like:

```
\x -> (\z -> z) (\y -> x b)
```

Let's start by writing a function to do the substitution:

```haskell
substituteIdForA :: String -> Exp String
substituteIdForA v
  | v == "a"  = ExpLam (ExpVar Nothing)
  | otherwise = ExpVar v
```

Let's use this with `bind`:

```haskell
> exampleExp1 >>= substituteIdForA :: Exp String
ExpLam $
  ExpApp
    (ExpLam (ExpVar Nothing))
    (ExpLam $
      ExpApp
        (ExpVar (Just Nothing))
        (ExpVar (Just (Just "b"))
    )
```

You can see how the `ExpVar (Just "a")` case has been replaced by the new
lambda expression.

Finally, let's look at `Foldable`[^3].  This gives us a way to pull out
all the free variables[^4]:

```
> foldMap (:[]) exampleExp1 :: [String]
["a", "b"]
```

This is a nice way to operate over all the free variables in an expression at
once.

## Takeaway for the *Bird and Paterson* Technique

The takeaway here is that once you come up with your expression data type:

```haskell
data Exp a
  = ExpVar a
  | ExpApp (Exp a) (Exp a)
  | ExpLam (Exp (Maybe a))
```

you can automatically derive `Functor`, `Foldable`, and `Traversable`.  The
`Applicative` instance is trivial.  All you really need to write is the `Monad`
instance, but it is straight-forward.

Despite being easy to write, these instances give you quite a lot of power for
manipulating free variables in your expressions.  This is pretty nice.[^44]

## Push it Further

While this is certainly nice, I started wondering if there was any way to push
it further.  Is there some formulation where you don't even have to write the
`Monad` instance?  Or you could derive it automatically somehow?

The first thing that came to my mind was a
[free-monad-like construction](https://serokell.io/blog/introduction-to-free-monads#free-monads-for-edsls).
With the normal approach to free monads for eDSLs, all you have to do is create
a datatype and derive `Functor` for it.  You then wrap it up in
[`Free`](https://hackage.haskell.org/package/free-5.2/docs/Control-Monad-Free.html#t:Free)
and you get the Monad instance... well, _for free_.

Could it work to just pass `Exp` to `Free`?  Maybe something like the following?

```haskell
data Free f a
  = Pure a
  | Free (f (Free f a))

data ExpFBad r
  = ExpFBadApp r r
  | ExpFBadLam (Maybe r)

type ExpBad = Free ExpFBad
```

This almost looks like it would work, but you'll notice that the `ExpFBadLam`
data constructor is incorrect.  Compare to `Exp`:

```haskell
data Exp a
  = ExpVar a
  | ExpApp (Exp a) (Exp a)
  | ExpLam (Exp (Maybe a))
```

In `ExpLam`, we're not recursing on `Maybe (Exp a)`, we're recursing on `Exp
(Maybe a)`.  As far as I can tell, it is not possible to express this type of
recursive datatype with `Free`[^5].

Let's rewrite `Exp` to make this a little more obvious:

```haskell
data ExpTran a
  = ExpTranVar a
  | ExpTranApp (IdentityT ExpTran a) (IdentityT ExpTran a)
  | ExpTranLam (MaybeT ExpTran a)
```

Here, instead of recursing directly in `Exp`, we've defined all recursion to
happen through the monad transformers `IdentityT` and `MaybeT`.  This `ExpTran`
datatype is isomorphic with `Exp`, but it shows better why it is not possible
to define using `Free` plus some functor.

So is there some sort of `Free`-like structure that we can use to define `Exp`?

## `FFree` and `ExpF` for `Exp`

After playing around with this a bunch, I came up with the following construction:



## Conclusion


## Footnotes

[^2]: It's another question whether an operation like this would mean anything
    _semantically_ for the underlying lambda calculus, but this is mostly just an
    example of using `fmap`.

[^3]: I'm not going to discuss `Traversable`, but it should be straight-forward
    to understand if you understand the `Foldable` instance.

[^4]: Note that `(:[])` is the `a -> [a]` singleton list construction function.
    Otherwise known as `pure` for the list monad.

[^44]: There are a few other advantages for the *Bird and Paterson* techique,
    You can find them in the
    [Bound](https://www.schoolofhaskell.com/user/edwardk/bound) article.

[^5]: I haven't formally proven this, but I played around with it a bunch and
    wasn't able to get it to work.  I plan on brushing off my dusty Coq
    skills and trying coming up with an actual proof of this.
